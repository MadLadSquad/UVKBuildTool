cmake_minimum_required(VERSION 3.21)

if (WIN32)
    set(LIBRARY_OUTPUT_PATH "${CMAKE_BINARY_DIR}")
    set(EXECUTABLE_OUTPUT_PATH "${CMAKE_BINARY_DIR}")
endif()

project(UVKBuildTool)

# This option is needed to define a compiler macro and the given source files to be used by the compilation target
option(UBT_COMPILING_FOR_WEB "If set to ON it will compile with configuration for a web project, otherwise will compile for the UntitledImGuiFramework!" OFF)
option(UBT_INSTALL "Whether to install the UVKBuildTool" OFF)

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/")

if (UBT_COMPILING_FOR_WEB)
    set(UBT_TEMPLATES_DIR "${CMAKE_SOURCE_DIR}/Templates/Web")
    set(UBT_INSTALL OFF)
else()
    if (NOT UBT_INSTALL)
        set(UBT_DATA_INSTALL_PREFIX "${CMAKE_SOURCE_DIR}")
        set(UBT_DIR "${CMAKE_SOURCE_DIR}")
        set(UBT_TEMPLATES_DIR "${UBT_DATA_INSTALL_PREFIX}/Templates/UntitledImGuiFramework")
    else()
        set(UBT_DIR "${UBT_DATA_INSTALL_PREFIX}/UVKBuildTool")
        set(UBT_TEMPLATES_DIR "${UBT_DIR}/Templates/UntitledImGuiFramework")
    endif ()
    if (NOT DEFINED UBT_FRAMEWORK_DIR)
        set(UBT_FRAMEWORK_DIR "../")
    endif()
    get_filename_component(UBT_FRAMEWORK_DIR "${UBT_FRAMEWORK_DIR}" REALPATH "${CMAKE_BINARY_DIR}")
endif()

add_subdirectory(rapidyaml)

if (WIN32)
    add_subdirectory(ucli/)
else()
    find_package(PkgConfig REQUIRED)
    pkg_check_modules(UntitledCLIParser UntitledCLIParser)
    if (UntitledCLIParser_FOUND)
        link_directories(${UntitledCLIParser_LIBRARY_DIRS})
        include_directories(${UntitledCLIParser_INCLUDE_DIRS})
    else()
        add_subdirectory(ucli/)
    endif()
endif()

if (UBT_COMPILING_FOR_WEB)
    set(BUILD_VARIANT_VENDOR ON)
    set(USE_PRECONFIGURED_YAML ON)
    set(RYML_INCLUDE_DIRS_T "rapidyaml/src/")

    add_subdirectory(ui18n)
endif()

include_directories(utg/src/ src/ ui18n/)

if (UBT_COMPILING_FOR_WEB)
    file(GLOB_RECURSE SRC "src/Web/*.cpp" )
    file(GLOB_RECURSE HEAD "src/Web/*.hpp" )
else()
    file(GLOB_RECURSE SRC "src/UntitledImGuiFramework/*.cpp")
    file(GLOB_RECURSE HEAD "src/UntitledImGuiFramework/*.hpp")
endif()
file(GLOB_RECURSE UNI_HEAD "src/Uniform/*.h" "utg/src/*.hpp" "utg/src/*.h" "ui18n/parallel-hashmap/*.h")
file(GLOB_RECURSE UNI_SRC "src/Uniform/*.cpp")
file(GLOB_RECURSE UTG_SRC "utg/src/*.cpp")

add_executable(UVKBuildTool ${SRC} ${UNI_SRC} ${UTG_SRC} ${HEAD} ${UNI_HEAD} src/main.cpp)

set_target_properties(UVKBuildTool PROPERTIES LINKER_LANGUAGE CXX)
set_target_properties(ryml PROPERTIES POSITION_INDEPENDENT_CODE ON)

target_compile_definitions(UVKBuildTool PRIVATE UBT_TEMPLATES_DIR="${UBT_TEMPLATES_DIR}" UBT_FRAMEWORK_DIR="${UBT_FRAMEWORK_DIR}" UBT_DIR="${UBT_DIR}" UTTE_CUSTOM_MAP=phmap::flat_hash_map UTTE_CUSTOM_MAP_INCLUDE=<parallel-hashmap/parallel_hashmap/phmap.h>)
if (UBT_COMPILING_FOR_WEB)
    target_compile_definitions(UVKBuildTool PRIVATE "UBT_TARGET_WEB")
else()
    target_compile_definitions(UVKBuildTool PRIVATE "UBT_TARGET_FRAMEWORK")
    if (UBT_INSTALL)
        target_compile_definitions(UVKBuildTool PRIVATE "UBT_DO_NOT_BUILD_FRAMEWORK")
    endif()
endif()

if (UBT_COMPILING_FOR_WEB)
    target_link_libraries(UVKBuildTool UntitledI18N)
endif()

target_link_libraries(UVKBuildTool UntitledCLIParser ryml c4core)
if (NOT UBT_COMPILING_FOR_WEB AND UBT_INSTALL)
    install(TARGETS UVKBuildTool DESTINATION bin/)
    install(DIRECTORY Templates/ DESTINATION share/UVKBuildTool/Templates)
endif ()
